apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/ChaosLeong/PinView'
def issueUrl = 'https://github.com/ChaosLeong/PinView/issues'
def gitUrl = 'git@github.com:ChaosLeong/PinView.git'

group = "com.chaos.view"
version = "1.4.4"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
    exclude '**/BuildConfig.class'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
    exclude '**/BuildConfig.class'
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
    exclude '**/BuildConfig.class'
}

afterEvaluate {
    publishing {
        publications {
            pinview(MavenPublication) {
                from components.release

                artifactId "pinview"

                artifact javadocJar
                artifact sourcesJar

                pom {
                    name = 'PinView'
                    description = 'Provides a widget for enter PIN/OTP/password etc.'
                    url = siteUrl

                    packaging = 'aar'

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                    developers {
                        developer {
                            id = 'ChaosLeong'
                            name = 'Chaos Leong'
                            email = 'lgf42031@gmail.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publications = ['pinview']
    pkg {
        repo = "maven"
        name = "PinView"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}